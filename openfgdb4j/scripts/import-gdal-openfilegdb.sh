#!/bin/zsh
set -euo pipefail

ROOT_DIR="$(cd "$(dirname "$0")/../.." && pwd)"
TP_DIR="$ROOT_DIR/openfgdb4j/third_party/gdal_openfilegdb"
SRC_DIR="$TP_DIR/src"
TMP_DIR="/tmp/ofgdb-gdal-import"
GDAL_TAG="v3.12.0"
ARCHIVE_URL="https://github.com/OSGeo/gdal/archive/refs/tags/${GDAL_TAG}.tar.gz"
ARCHIVE_FILE="$TMP_DIR/gdal.tar.gz"
LOCAL_ARCHIVE_CACHE="/tmp/ofgdb-gdal-cache/gdal-${GDAL_TAG}.tar.gz"
IMPORT_SUBDIR="ogr/ogrsf_frmts/openfilegdb"

rm -rf "$TMP_DIR"
mkdir -p "$TMP_DIR" "$SRC_DIR"

if [[ -n "${OPENFGDB4J_GDAL_ARCHIVE:-}" && -f "${OPENFGDB4J_GDAL_ARCHIVE}" ]]; then
  cp "${OPENFGDB4J_GDAL_ARCHIVE}" "$ARCHIVE_FILE"
elif [[ -f "$LOCAL_ARCHIVE_CACHE" ]]; then
  cp "$LOCAL_ARCHIVE_CACHE" "$ARCHIVE_FILE"
else
  curl -fL "$ARCHIVE_URL" -o "$ARCHIVE_FILE"
fi

mkdir -p "$(dirname "$LOCAL_ARCHIVE_CACHE")"
cp "$ARCHIVE_FILE" "$LOCAL_ARCHIVE_CACHE"

tar -xzf "$ARCHIVE_FILE" -C "$TMP_DIR"

IMPORT_ROOT="$TMP_DIR/gdal-${GDAL_TAG#v}"
if [[ ! -d "$IMPORT_ROOT" ]]; then
  IMPORT_ROOT="$TMP_DIR/gdal-${GDAL_TAG}"
fi

if [[ ! -d "$IMPORT_ROOT" ]]; then
  echo "Failed to locate extracted GDAL directory for tag $GDAL_TAG" >&2
  exit 1
fi

IMPORT_TREE="$IMPORT_ROOT/$IMPORT_SUBDIR"
if [[ ! -d "$IMPORT_TREE" ]]; then
  echo "Missing expected source directory: $IMPORT_SUBDIR" >&2
  exit 1
fi

find "$SRC_DIR" -mindepth 1 -delete
mkdir -p "$SRC_DIR/ogr/ogrsf_frmts"
cp -R "$IMPORT_TREE" "$SRC_DIR/ogr/ogrsf_frmts/"

typeset -a FILES
FILES=(${(f)"$(cd "$SRC_DIR" && find "$IMPORT_SUBDIR" -type f | sort)"})

{
  echo "# Imported from OSGeo/gdal tag $GDAL_TAG"
  echo "# Generated by scripts/import-gdal-openfilegdb.sh"
  for rel in "${FILES[@]}"; do
    echo "$rel"
  done
} > "$TP_DIR/IMPORTED_FILES.txt"

echo "Imported ${#FILES[@]} GDAL OpenFileGDB files from $GDAL_TAG"
